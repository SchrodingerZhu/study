# 函子

## 定义
对于装在容器`f`中的元素`a`, 我们拥有一个`a -> b`的函数，现在我们想利用这个函数构造一个新的函数`f a -> f b`, 满足这种构造的容器，可以抽象为函子。

```haskell
class Functor f where
    fmap :: (a -> b) -> f a -> f b
```
`fmap`把对应元素的函数提升到了容器级别

一些简单的实现有
```haskell
instance Functor [] where
    fmap = map

instance Functor Maybe where
    fmap f (Just a) = Just (f a)
    fmap _ Nothing = Nothing

instance Functor ((,) a) where
    fmap f (x, y) = (x, f y)
```

一个比较重要的函子是
```haskell
instance Functor ((->) a) where
    fmap = (.)
```
即`a -> b`的函数可以看作一个装载`b`的容器

## 简单范畴论
范畴描述的是一群对象和他们之间的态射关系。简单说，一个范畴包括
- 对象类
- 态射类
- 组合二元运算，满足
  - 结合律
  - 单位元存在

这里的类是在罗素悖论提出后建立新的集合论公理时提出的一个约束弱于集合的同样描述一群物体的概念

在Haksell中，类型构成`Hask`范畴，函数即是态射，组合运算为`(.)`，这就要求
```haskell
fmap id = id
fmap (f . g) = fmap f . fmap g
```

## Identity 函子
Identity 做的就是一个简单的打包
```haskell
newtype Identity a =  Identity {runIdentity :: a} 

instance Functor Identity where
    fmap f = Identity . f . runIdentity
```

## Const 函子
先观察`Const`定义
```haskell
newtype Const a b = Const {getConst :: a}

Const 3 :: Const Int String
Const 3 :: Const Int Int
```
这种左侧类型没有出现在右侧构造函数中的类型称为幻影类型，它可以额外携带一个类型信息
它的函子定义：

```haskell
instance Functor (Const a) where
    fmap f c = c

-- fmap :: (b -> c) -> Const a b -> Const a c
```

## IO 函子
IO 也是一个函子，所以可以有以下这种操作
```haskell
module Test where
main :: IO ()
main = do
    l <- fmap length getLine
    print l
```
