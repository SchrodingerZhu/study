# 透镜组
透镜组充分利用了`Identity`和`Const`函子的作用。一般语言中都有数据结构的`getter`和`setter`，但是想要做连续的操作，普通的`getter`和`setter`就显得非常费事，这时就应当考虑透镜组的作用了

## 示例
``` haskell
{-# LANGUAGE RankNTypes #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE MonoLocalBinds #-}
module MyLens where

import Data.Functor.Identity
import Data.Functor.Const
data Position = Position {getX::Double, getY::Double} 
data Line = Line { getS::Position, getT::Position }
type Lens f a b = Functor f => (a -> f a) -> b -> f b

xLens :: Functor f => Lens f Double Position
xLens f p = fmap (\x' -> setX x' p) $ f (getX p)
    where 
        setX :: Double -> Position -> Position
        setX x' p = p { getX = x'}

yLens :: Functor f => Lens f Double Position
yLens f p = fmap (\y' -> setY y' p) $ f (getY p)
    where 
        setY :: Double -> Position -> Position
        setY y' p = p { getY = y'}

sLens :: Functor f => Lens f Position Line
sLens f line = fmap (\s' -> line { getS = s' }) $ f $ getS line

tLens :: Functor f => Lens f Position Line
tLens f line = fmap (\t' -> line { getT = t' }) $ f $ getT line

over :: (Lens Identity a b) -> (a -> a) -> b -> b
over lens f x = runIdentity $ lifted x
        where 
            lifted = lens (Identity . f)

set :: (Lens Identity a b) -> a -> b -> b
set lens a' x = over lens (\_ -> a') x

view :: (Lens (Const a) a b) -> b -> a
view lens x = getConst $ (lens Const) x

instance Show Position where
    show p = "<" ++ (show $ getX p) ++ ", " ++ (show $ getY p) ++ ">" 

instance Show Line where
    show l = (show $ getS l) ++ " => " ++ (show $ getT l)
```

## Lens定义
```haskell
type Lens f a b = Functor f => (a -> f a) -> b -> f b
```
观察这一段定义，`a`代表外界想要修改的内部数据，`b`是`a`的载体，`f`是过程中用来打包的外层函子
这里我们需要手动书写这些透镜组的定义可能比较烦，但是很多函数库提供了基于泛型技巧的自动生成透镜组的方法。

## Lens书写
观察具体的实例
```haskell
xLens :: Functor f => Lens f Double Position
xLens f p = fmap (\x' -> setX x' p) $ f (getX p)
    where 
        setX :: Double -> Position -> Position
        setX x' p = p { getX = x'}
```
这里我们把一个更新`x`坐标的函数首先升格到外层函子，然后应用在函子包装的原有`x`坐标上。

## 辅助函数
### over
首先看`over`函数,它的目的是把一个修改内部数据的函数apply到容器内部，得到修改后的容器，即
```haskell
over xLens (+1) (Position 1 2) = Position 2 2
```
他的定义如下

```haskell
over :: (Lens Identity a b) -> (a -> a) -> b -> b
over lens f x = runIdentity $ lifted x
        where 
            lifted = lens (Identity . f)
```
相当于用一个`Identity . f`包裹了数据，自然也就应用的函数。然后外层进行`runIdentity`解包。

### set
`set` 作用是替换值，使用`over`可以很容易的实现
```haskell
set :: (Lens Identity a b) -> a -> b -> b
set lens a' x = over lens (\_ -> a') x
```

### view
`view` 作用是得到对应值，这里需要用的`Const`函子。考虑装载`a`的容器`b`，我们把它包裹成`Const a b`，这样就可以提取出其中的`a`。
```haskell
view :: (Lens (Const a) a b) -> b -> a
view lens x = getConst $ (lens Const) x
```

## 透镜组合
`Line`包装了`Position`，可以用透镜组合的方式来实现对内层的修改和访问
```haskell
view (tLens . yLens) someLine
```