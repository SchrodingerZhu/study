# 模板泛型

## 模板编程

### 简介

`lens`这个库中在`Control.Lens.HT`下提供了一个`makeLenses`函数，可以自动为写好的类型生成透镜组，这就用到了模板编程。

### AST
`haskell`内置了自己的`AST`定义，一些简单的部分包括：
```haskell
data Exp 
    = VarE Name -- 变量
    | ConE Name -- 构造函数
    | AppE Exp Exp -- 函数应用
    | InfixE (Maybe Exp) Exp (Maybe Exp) -- 中缀函数应用/Section语法
    | LitE Lit -- 字面量 
```

为了流畅的使用这些定义，需要引入`template-haskell`库以及`Language.Haskell.TH.Syntax`,并打开`TemplateHaskell`. 同时也就得到了很多辅助函数，如`mkName`就能够把`String`转换到`Name`.

在语法方面，主要注意：
- 在普通绑定前面加上`'`可以得到对应的`Name`，`'fmap = fmap :: Name`
- 在类型和类型类前面加上`''`可以得到对应的`Name`, `''Position = Position :: Name`

此外还有很多辅助定义：
- 类型
  ```haskell
    data Type = ForallT [TyVarBndr] Cxt Type  -- ^ @forall \<vars\>. \<ctxt\> -> \<type\>@
          | AppT Type Type                -- ^ @T a b@
          | SigT Type Kind                -- ^ @t :: k@
          | VarT Name                     -- ^ @a@
          | ConT Name                     -- ^ @T@
          | PromotedT Name                -- ^ @'T@

          -- See Note [Representing concrete syntax in types]
          | TupleT Int                    -- ^ @(,), (,,), etc.@
          | UnboxedTupleT Int             -- ^ @(#,#), (#,,#), etc.@
          | ArrowT                        -- ^ @->@
          | ListT                         -- ^ @[]@
          | PromotedTupleT Int            -- ^ @'(), '(,), '(,,), etc.@
          | PromotedNilT                  -- ^ @'[]@
          | PromotedConsT                 -- ^ @(':)@
          | StarT                         -- ^ @*@
          | ConstraintT                   -- ^ @Constraint@
          | LitT TyLit                    -- ^ @0,1,2, etc.@
      deriving( Show, Eq, Data, Typeable )
  ```
- 模式
  ```haskell
  data Pat 
    = LitP Lit                      -- ^ @{ 5 or 'c' }@
    | VarP Name                     -- ^ @{ x }@
    | TupP [Pat]                    -- ^ @{ (p1,p2) }@
    | UnboxedTupP [Pat]             -- ^ @{ (# p1,p2 #) }@
    | ConP Name [Pat]               -- ^ @data T1 = C1 t1 t2; {C1 p1 p1} =    e@
    | InfixP Pat Name Pat           -- ^ @foo ({x :+ y}) = e@
    | UInfixP Pat Name Pat          -- ^ @foo ({x :+ y}) = e@
                                    --
                                    -- See    "Language.Haskell.TH.Syntax#infix"
    | ParensP Pat                   -- ^ @{(p)}@
                                    --
                                    -- See    "Language.Haskell.TH.Syntax#infix"
    | TildeP Pat                    -- ^ @{ ~p }@
    | BangP Pat                     -- ^ @{ !p }@
    | AsP Name Pat                  -- ^ @{ x \@ p }@
    | WildP                         -- ^ @{ _ }@
    | RecP Name [FieldPat]          -- ^ @f (Pt { pointx = x }) = g x@
    | ListP [ Pat ]                 -- ^ @{ [1,2,3] }@
    | SigP Pat Type                 -- ^ @{ p :: t }@
    | ViewP Exp Pat                 -- ^ @{ e -> p }@
    deriving( Show, Eq, Data, Typeable )
  ```
- 声明
  ```haskell
  data Dec 
      = FunD Name [Clause]            -- ^ @{ f p1 p2 = b where decs }@
    | ValD Pat Body [Dec]           -- ^ @{ p = b where decs }@
    | DataD Cxt Name [TyVarBndr] 
           [Con] [Name]             -- ^ @{ data Cxt x => T x = A x | B (T    x)
                                    --       deriving (Z,W)}@
    | NewtypeD Cxt Name [TyVarBndr] 
           Con [Name]               -- ^ @{ newtype Cxt x => T x = A (B x)
                                    --       deriving (Z,W)}@
    | TySynD Name [TyVarBndr] Type  -- ^ @{ type T x = (x,x) }@
    | ClassD Cxt Name [TyVarBndr] 
           [FunDep] [Dec]           -- ^ @{ class Eq a => Ord a where ds }@
    | InstanceD Cxt Type [Dec]      -- ^ @{ instance Show w => Show [w]
                                    --       where ds }@
    | SigD Name Type                -- ^ @{ length :: [a] -> Int }@
    | ForeignD Foreign              -- ^ @{ foreign import ... }
                                    --{ foreign export ... }@

    | InfixD Fixity Name            -- ^ @{ infix 3 foo }@

    -- | pragmas
    | PragmaD Pragma                -- ^ @{ {\-# INLINE [1] foo #-\} }@

    -- | type families (may also appear in [Dec] of 'ClassD' and  'InstanceD')
    | FamilyD FamFlavour Name 
           [TyVarBndr] (Maybe Kind) -- ^ @{ type family T a b c :: * }@

    | DataInstD Cxt Name [Type]
           [Con] [Name]             -- ^ @{ data instance Cxt x => T [x] =    A x 
                                    --                                | B     (T x)
                                    --       deriving (Z,W)}@
    | NewtypeInstD Cxt Name [Type]
           Con [Name]               -- ^ @{ newtype instance Cxt x => T [x]   = A (B x)
                                    --       deriving (Z,W)}@
    | TySynInstD Name [Type] Type   -- ^ @{ type instance T (Maybe x) = (x,   x) }@
    deriving( Show, Eq, Data, Typeable )
  ```
## 例子1

手动生成以下定义的函数：
```haskell
length :: [a] -> Int
length [] = 0
length (x:xs) = 1 + length xs
```

相关类型的构造函数：
```haskell
data Clause = Clause [Pat] Body [Dec]
data Body = GuardedB [(Guard, Exp)] | NormalB Exp
```
示例代码：
```haskell
{-# LANGUAGE TemplateHaskell #-}

module Test where

import Language.Haskell.TH
import Language.Haskell.TH.Syntax
makeLengthTH :: [Dec]
makeLengthTH = [typeSig, FunD lengthTH [eq1, eq2]]
  where
    lengthTH = mkName "lengthTH"
    typeSig = SigD lengthTH (ForallT
        [PlainTV (mkName "a")]
        []
        (ArrowT `AppT` (AppT ListT (VarT $ mkName "a")) `AppT` (ConT ''Int))
      )

    eq1 = Clause
      [ConP '[] []]
      (NormalB (LitE $ IntegerL 0))
      []

    eq2 = Clause
      [InfixP (VarP $ mkName "x") '(:) (VarP $ mkName "xs")]
      (NormalB (
          InfixE
            (Just (LitE $ IntegerL 1))
            (VarE '(+))
            (Just $ (VarE lengthTH)) `AppE` (VarE $ mkName "xs")))
      []

{-
putStrLn $ pprint makeLengthTH
lengthTH :: forall a . [a] -> GHC.Types.Int
lengthTH (GHC.Types.[]) = 0
lengthTH (x GHC.Types.: xs) = (1 GHC.Num.+ lengthTH) xs
-}
```


### Q 单子

这里的`lengthTH`只是生成了合适的`Haskell`代码，但是我们还并没有的到真正可用的函数。考察`makeLenses`这类函数的定义，会发现实际类型是套在`Q`单子中的：
```haskell
type DecsQ = Q [Dec]
makeLenses :: Name -> DecsQ
```
`Q`单子用于编译期处理模板代码，主要功能有：
- 产生和运行时隔离的`Name`: 
  ```haskell
  newName :: String -> Q Name
  ```
- 获取某一个`Name`的定义信息，如构造函数：
  ```haskell
  reify :: Name -> Q Info
  ```
- 获取模板代码执行的位置：
  ```haskell
  location :: Q Loc
  ```
- 分析并打印模板错误：
  ```haskell
  reportError :: String -> Q ()
  source, reportWarning :: String -> Q ()
  ```
- 编译期`IO`：
  ```haskell
  runIO :: IO a -> Q a
  ```

### 牛津括号
- `[|...|], [e|...|]`将括号内容解析为`Q Exp`
- `[t|...|]`, 将括号内容解析为`Q Type`
- `[p|...|]`, 将括号内容解析为`Q Pat`
- `[d|...|]`, 将括号内容解析为`Q Dec`
使用`QuasiQuotes`拓展可以允许按照`[yourParser|...|]`这样调用自己的函数来解析程序块。

其中`yourParser`应当符合`QuasiQuoter`的定义：
```haskell
data QuasiQuoter = QuasiQuoter {
  quoteExp :: String -> Q Exp,
  quotePat :: String -> Q Pat,
  quoteType :: String -> Q Type,
  quoteDec :: String -> Q [Dec]
}
```
定义至少一种函数即可。


### 拼接
`$...`和`$(...)`在开启模板后就相当于`unquote`，把相应的模板代码拼接回可执行的代码
```haskell
$(litE (integerL 1))  == 1
```
需要注意的是`litE, varP,integerL`这写函数都是`Language.Haskell.TH.Lib`中定义的辅助函数，直接返回`Q`单子版的对应结构


### reify

在GHCi中可以通过一下方法查看`reify`得到的内容：
```haskell
:{
data Position = Position {
  _x :: Double,
  _y :: Double
}
:}

putStrLn $ $(reify '' Position >>= stringE . pprint)

{- output:
data Ghci8.Position
    = Ghci8.Position {Ghci8._x :: GHC.Types.Double,
                      Ghci8._y :: GHC.Types.Double}
-}
```

### 例子：简化版`makeLenses`

```haskell
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE RankNTypes #-}
module MakeLenses where

import Language.Haskell.TH
import Control.Monad
import Language.Haskell.TH.Syntax
import Language.Haskell.TH.Lib

mkVars :: String -> Q (PatQ, ExpQ)
mkVars name = do
  x <- newName name
  return (varP x, varE x)

makeLenses :: Name -> DecsQ
makeLenses typeName = do
  TyConI (DataD _ _ _ _ cons _) <- reify typeName
  [RecC conName fields] <- return cons -- assume only one constructor
  fmap concat $
    forM fields $ \(fieldName, _, fieldType) ->
                    case nameBase fieldName of
                      ('_':rest) ->
                        makeLens typeName conName (mkName rest) fieldName fieldType
                      _ -> return []

makeLens
  :: Name -- dataType
  -> Name -- constructorName
  -> Name -- lensName
  -> Name -- fieldName
  -> Type -- fieldType
  -> DecsQ
makeLens typeName conName lensName fieldName fieldType = do
  let bT = conT typeName
      aT = return fieldType
      lensT = [t|forall f. (Functor f) => ($aT -> f $aT) -> $bT -> f $bT |]
  sig <- sigD lensName lensT

  (fP, fE) <- mkVars "f"
  (bP, bE) <- mkVars "b"
  (xP, xE) <- mkVars "x"
  xE' <- xE

  let
     lam = [| \ $xP -> $(recUpdE bE [return (fieldName, xE')])|]
     pats = [fP, bP]
     rhs = [|fmap $lam ($fE ($(varE fieldName) $bE))|]

  body <- funD lensName [clause pats (normalB rhs) []]

  return [sig, body]

```

## 泛型编程

### 和类型

对于`data Either a b = Left a | Right b`这种类型定义，可能的类型总数是`a`和`b`可能类型数的和，称为**和类型**.

### 积类型

对于`data (,) a b = (a, b)` 这样的定义，可能的类型总数是`a`和`b`可能类型数的积，称为**积类型**.

### Generic
通过和类型和积类型的抽象，可以泛化某些函数，这就是`Haskell`的泛型思想，通用表示方法定义在`GHC.Generics`里：

```haskell
-- | Unit: used for constructors without arguments
data U1 p = U1

-- | Constants, additional parameters and recursion of kind *
newtype K1 i c p = K1 { unK1 :: c }

-- | Meta-information (constructor names, etc.)
newtype M1 i c f p = M1 { unM1 :: f p }

-- | Sums: encode choice between constructors
infixr 5 :+:
data (:+:) f g p = L1 (f p) | R1 (g p)

-- | Products: encode multiple arguments to constructors
infixr 6 :*:
data (:*:) f g p = f p :*: g p
```

打开`DeriveGenerics`后就可以让编译器自动推导出相应的泛型定义。

```haskell
class Generic a where
  type Rep a :: * -> *
  from :: a -> Rep a x
  to :: Rep a x -> a
```

例如
```haskell
data Tree a = Leaf a | Node (Tree a) (Tree a) deriving (Show, Generic)

-- ghci
let m = Leaf 1
from m 

-- M1 {unM1 = L1 (M1 {unM1 = M1 {unM1 = K1 {unK1 = 1}}})}


let n = Node (Leaf 1) (Leaf 2)
from n

-- M1 {unM1 = R1 (M1 {unM1 = M1 {unM1 = K1 {unK1 = Leaf 1}} :*: M1 {unM1 = K1 {unK1 = Leaf 2}}})}

```

真正需要关心的是如何使用泛型编程，示例（树和比特串互相转化的例子）：

```haskell
{-#LANGUAGE DeriveGeneric#-}
{-# LANGUAGE TypeOperators #-}
{-# LANGUAGE DefaultSignatures #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE FlexibleInstances #-}
module Gen where
import GHC.Generics

data Bit = O | I deriving (Show, Eq, Ord)

class GSerialize f where
  gput :: f a -> [Bit]
  gget :: [Bit] -> (f a, [Bit])

instance GSerialize U1 where
  gput U1 = []
  gget xs = (U1, xs)

instance (GSerialize a, GSerialize b) => GSerialize (a :*: b) where
  gput (x :*: y) = gput x ++ gput y
  gget xs = (a :*: b, xs'')
    where (a, xs') = gget xs
          (b, xs'') = gget xs'

instance (GSerialize a, GSerialize b) => GSerialize (a :+: b) where
  gput (L1 x) = O : gput x
  gput (R1 x) = I : gput x
  gget (O:xs) = (L1 x, xs')
    where (x, xs') = gget xs
  gget (I:xs) = (R1 x, xs')
    where (x, xs') = gget xs

instance (GSerialize a) => GSerialize (M1 i c a) where
  gput (M1 x) = gput x
  gget xs = (M1 x, xs')
    where (x, xs') = gget xs

instance (Serialize a) => GSerialize (K1 i a) where
  gput (K1 x) = put x
  gget xs = (K1 x, xs')
    where (x, xs') = get xs

class Serialize a where
  put :: a -> [Bit]
  get :: [Bit] -> (a, [Bit])

  default put :: (Generic a, GSerialize (Rep a)) => a -> [Bit]
  put a = gput (from a)

  default get :: (Generic a, GSerialize (Rep a)) => [Bit] -> (a, [Bit])
  get xs = (to x, xs')
    where (x, xs') = gget xs

instance Serialize Bool where
  put True = [I]
  put False = [O]
  get (I:xs) = (True, xs)
  get (O:xs) = (False, xs)

data Tree a = Leaf a | Node (Tree a) (Tree a) deriving (Show, Generic)

instance Serialize (Tree Bool) 
```

```haskell
put $ from (Leaf False)
-- [O,O]
put $ from (Leaf True)
-- [O,I]
put $ from (Node (Leaf False) (Leaf True))
[I,O,O,O,I]
fst (get [I,O,O,O,I]) :: Tree Bool
-- Node (Leaf False) (Leaf True)
```