# 单子实例
## Reader

我们把`(->) a`类型的函子称为`Reader`函子，对应地也能给出单子的定义。`Reader`的应用函子作用是通过`<*>`向所连接的函数传递相同的参数然后传给一开始升格的函数得出最后的结果。

示例：
```haskell
input :: InputType
input = ...

let a = funcA input
    b = funcB input
    c = funcC input
in  final a b c

-- or

final <$> funcA <*> funcB <*> funcC $ input
```

在应用函子层面上，存在以下限制：
- 最终传入的升格函数的格式必须事先确定，且单个子函数直接数据不能互相依赖
- 不能中途对传入数据加以修改

因此，可以把`Reader`升级为单子的形式，这样可以提高灵活度。

```haskell
instance Monad ((->) a) where
    return = pure
    f >>= g = \x -> g (f x) x
```

考虑构建一个数据结构
```haskell
data MyStruct = MyStruct {
    foo :: String
,   bar :: String
,   zyf :: String
} deriving Show
```
同时有三个帮助加工字符串的函数，
```haskell
prepareFoo, prepareBar, prepareZYF :: String -> String
```
现在我们就可以书写出从一个字符串构造这个数据结构的方法：
```haskell
construct :: String -> MyStruct
construct = MyStruct <$> prepareFoo <*> prepareBar <*> prepareZYF 
```
使用单子可以写为
```haskell
construct' = do
    f <- prepareFoo
    b <- prepareBar
    z <- prepareZYF
    return $ MyStruct f b z 
```

为了方便使用，可以定义如下函数：
```haskell
ask :: a -> a
ask = id

local :: (a -> a) -> (a -> r) -> a -> r
local f g = g . f
```
第一个函数可以用在开头，显式的得出“全局参数”；第二个函数则可以用于在局部修改参数，如
```haskell
test = do 
    n <- ask
    f <- local (const "xxx") prepareFoo
    b <- prepareBar
    return $ MyStruct f b n
```
如果想把`local`影响的环境扩大，就在`local`后面加上一个子级`do-block`.

### 新类型形式
暂时忽略单子变换的问题，以直白的方式可以定义`Reader`为：
```haskell
newtype Reader r a = Reader {runReader :: r -> a}

instance Functor (Reader r) where
    fmap f m = Reader $ \r -> f (runReader m r)

instance Applicative (Reader r) where
    pure a = Reader $ \_ -> a
    f <*> x = Reader $ \r -> runReader f r $ runReader x r

instance Monad (Reader r) where
    return = pure
    m >>= k = Reader $ \r -> runReader (k (runReader m r)) r
```

## State
一般地，我们可以把一段程序看作一台转换状态的机器，输入一个初始状态，得到一个输出以及转换后的状态：
```haskell
stateA -> (output, stateB)
```
在其它语言中，我们把状态看成所有的全局变量的总和，程序每执行一步，就会对自身的状态做出更改。而在`haskell`中，单子也可以承载一段上下文，这样我们就可以专门抽象出一种单子来处理状态间的转移。

```haskell
newtype State s a = State { runState :: s -> (a, s) } 
```

作为函子

```haskell
instance Functor (State s) where
    fmap f fs = State $ \s ->
        let (a, s') = runState fs s
        in (f a, s')
```
大致过程就是转移状态，获得输出，加工输出，包裹回`State`

作为应用函子

```haskell
instance Applicative (State s) where
    pure a = State $ \s -> (a, s)
    f <*> fa = Sfate $ \s ->
        let (fab, s0) = runState f s
            (a, s1) = runState fa s0
        in (fab a, s1)
```

作为单子
```haskell
instance Monad (State s) where
    return = pure
    fa >>= f = State $ \s ->
        let (a, s') = runState fa s
        in runState (f a) s'
```

几个辅助函数
```haskell
get :: State s s
get = State $ \s -> (s, s)
-- 输出state本身


put :: s -> State s ()
put s = State $ \_ -> ((), s)
-- 直接放入state 

modify :: (s -> s) -> State s ()
modify f = State $ \s -> ((), f s)
-- 直接修改state

gets :: (s -> a) -> State s a
gets f = State $ \s -> (f s, s)
-- 按函数输出state

evalState :: State s a -> s -> a
evalState act = fst . runState act
-- 返回最终值

execState :: State s a -> s -> s
execState act = snd . runState act
-- 返回最终态
```

### 示例1
```haskell
module StateGame where

import Control.Monad.State

-- Example use of State monad
-- Passes a string of dictionary {a,b,c}
-- Game is to produce a number from the string.
-- By default the game is off, a C toggles the
-- game on and off. A 'a' gives +1 and a b gives -1.
-- E.g 
-- 'ab'    = 0
-- 'ca'    = 1
-- 'cabca' = 0
-- State = game is on or off & current score
--       = (Bool, Int)

type GameValue = Int
type GameState = (Bool, Int)

playGame :: String -> State GameState GameValue
playGame []     = do
    (_, score) <- get
    return score

playGame (x:xs) = do
    (on, score) <- get
    case x of
         'a' | on -> put (on, score + 1)
         'b' | on -> put (on, score - 1)
         'c'      -> put (not on, score)
         _        -> put (on, score)
    playGame xs

startState = (False, 0)

main = print $ evalState (playGame "abcaaacbbcabbab") startState
```

### 示例2
使用state计算a的个数
```haskell
countA :: String -> State Int Int
countA [] = get

countA (x:xs) = do
    case x of
        'a' -> modify (+1)
        _  -> modify id
    countA xs

-- evalState (countA "aaaa") $ 0
-- 4
```

### 示例3
欧拉筛法
```haskell
{-# LANGUAGE MultiWayIf #-}
{-# LANGUAGE TemplateHaskell #-}
module Main where
import Control.Monad.State.Strict
import Data.HashSet
import Control.Lens.TH
import Control.Lens
import Data.Sequence (Seq (Empty, (:|>), (:<|)))
data GenState = GenState {
  _lensMax :: Int,
  _lensPrimes :: Seq Int,
  _lensNow :: Int,
  _lensMarks :: HashSet Int
}

makeLenses ''GenState

initState :: Int -> GenState
initState x = GenState x Data.Sequence.Empty 2 empty

generatorInner :: State GenState (Seq Int)
generatorInner = do
  state' <- get
  let bound = view lensMax state'
      primes = view lensPrimes state'
      now = view lensNow state'
      marks = view lensMarks state'
      update :: (Seq Int) -> State GenState ()
      update Data.Sequence.Empty = return ()
      update (j :<| js) =
        if j * now > bound then return ()
        else do
          modify (over lensMarks (insert (j * now)))
          if now `mod` j == 0 then return ()
          else update js
  if | now > bound -> return primes
     | not $ member now marks -> do
         modify (over lensPrimes $ (:|> now))
         update $ primes :|> now
         modify (over lensNow (+1))
         generatorInner
     | otherwise -> do
         update $ primes
         modify (over lensNow (+1))
         generatorInner

main :: IO ()
main = do
  n <- readLn
  print $ evalState generatorInner (initState n)
```

## IO 
每次输入输出都是要进行处理的，对外部世界产生影响。如果仅仅返回一个简单的`()`类型，那么在纯函数的角度每次执行的返回值都是一样的，即无需进行第二次执行，这显然和`IO`操作不符合。所以为了实现这种包含副作用的过程，需要定义一种类型标注出每次返回的东西会有所不同，这也正是`IO`单子的作用。

```haskell
newtype IO = IO a (State# RealWorld -> (# State# RealWorld a #))
```

可见，`IO`的形式和`State`很是相近，实际上，他们的函子、应用函子和单子的定义也很相近：
```haskell
instance Functor IO where
    fmap f x = x >>= (return . f)

instance Applicative IO where
    pure = return 
    (<*>) = ap

instance Monad IO where
    m >> k = m >>= \_ -> k
    return = returnIO
    (>>=) = bindIO

returnIO :: a -> IO a
returnIO x = IO $ \x -> (# s, x #)

bindIO :: IO a -> (a -> IO b) -> IO b
bindIO (IO m) k = IO $ \s -> case m s of (# new_s, a#) -> unIO (k a) new_s
```

这样我们就可以把`IO`的函数理解为一个个转移外部世界条件的操作了。更特别的，所有交互程序运行时需要一个`RealWorld`，这个起始状态由`main`函数传入。

### 基本函数

```haskell
-- console write
putChar :: Char -> IO ()
putStr :: String -> IO ()
putStrLn :: String -> IO ()
print :: Show a => a -> IO ()
-- console read 
getChar :: IO Char
getLine :: IO String
getContents :: IO String
interact :: (String -> String) -> IO () -- 使用交互函数
-- console parse
readIO :: Read a => String -> IO a
readLn :: Read a => IO a
readMaybe :: Read a => String -> Maybe a
-- file
type FilePath = String
readFile :: FilePath -> IO String
writeFile :: FilePath -> String -> IO ()
appendFile :: FilePath -> String -> IO ()
```

`IO`操作自动非阻塞，运行时交给`IO manager`执行。

以下函数：
```haskell
copyFile f1 f2 = do
    c <- readFile f1
    writeFile f2 c
```
在一般语言中是不能使用的因为文件很大时会被一块读进来，从而占满内存；但是因为haskell本身就是惰性环境，这样写就是可以的。需要即时读取时，可以使用`deepseq`等函数

### IO 变量
在`RealWorld`中我们甚至可以存变量！
在`Data.IORef`中可以引入：
```haskell
data IORef a = {-HOLE-}

newIORef :: a -> IO (IORef a) -- 新建变量
readIORef :: IORef a -> IO a -- 读取变量
writeIORef :: IORef a -> a -> IO () -- 修改变量
modifyIORef :: IORef a -> (a -> a) -> IO () -- 按函数修改变量
modifyIORef' :: IORef a -> (a -> a) -> IO () -- 立即运行的按函数修改变量
```

### 并发IO
`Haskell`也采用轻量级线程，在`IO`中可以使用`forkIO`来开辟一个新的线程
```haskell
import Control.Concurrent
import Control.Monad
-- forkIO :: IO () -> IO ThreadIO

main :: IO ()
main = do
    tid1 <- forkIO $ forever $ do
        threadDelay 1000000
        putStrLn "hello"
    threadDelay 500000
    tid2 <- forkIO $ forever $ do
        threadDelay 1000000
        putStrLn "world"
    putStrLn $ "runing at " ++
      show tid1 ++ " and " ++ show tid2 ++ ". "
    threadDelay 100000000
```
同时还提供了原子操作：
```haskell
atomicModifyIORef' :: IORef a -> (a -> (a, b)) -> IO b
atomicModifyIORef :: IORef a -> (a -> (a, b)) -> IO b
atomicWriteIORef :: IORef a -> a -> IO ()
```

### Unsafe IO
在`System.IO.Unsafe`中有这样一个函数
```haskell
unsafePerformIO :: IO a -> a
-- 凭空取出的IO中的值
```
这种函数有时候必不可少但是滥用的话类型安全会被破坏。

### 面向IO单子的双向链表
```haskell
{-# LANGUAGE RecursiveDo #-}
module Main where
import Data.IORef
import System.IO.Unsafe
data Node a = Node {
  prev :: (IORef (Node a)),
  ele :: Maybe a,
  next :: (IORef (Node a))
} | Nil

data List a = List {
  root :: (IORef (Node a)),
  size :: IORef Int
}

newNode :: Maybe a -> IO (Node a)
newNode x = do
  a    <- newIORef Nil
  b    <- newIORef Nil
  return $ Node a x b

newList :: IO (List a)
newList = do
  node <- newNode Nothing
  setPrev node node
  setNext node node
  n    <- newIORef node
  s    <- newIORef 0
  return $ List n s

setPrev :: Node a -> Node a -> IO ()
setPrev n p = writeIORef (prev n) p

setNext :: Node a -> Node a -> IO ()
setNext n p = writeIORef (next n) p

cons :: List a -> a -> IO ()
cons l a = do
  r    <- readIORef (root l)
  n    <- readIORef (next r)
  node <- newNode $ Just a
  setPrev node r
  setNext node n
  setPrev n node
  setNext r node
  modifyIORef' (size l) (+1)


snoc :: List a -> a -> IO ()
snoc l a = do
  r    <- readIORef (root l)
  n    <- readIORef (prev r)
  node <- newNode $ Just a
  setPrev node n
  setNext node r
  setPrev r node
  setNext n node
  modifyIORef' (size l) (+1)

printList :: Show a => List a -> IO ()
printList l = do
  r' <- readIORef (root l)
  r  <- readIORef (next r')
  s  <- readIORef (size l)
  runPrint r s
  where
    runPrint :: Show a => Node a -> Int -> IO ()
    runPrint _ 0 = return ()
    runPrint Nil _ = return ()
    runPrint (Node _ e n) k = do
      print e
      m <- readIORef n
      runPrint m (k - 1)

fromList :: [a] -> IO (List a)
fromList l = do
  n <- newList
  runList l n
  return n
  where
    runList :: [a] -> List a -> IO ()
    runList [] _     = return ()
    runList (x:xs) m = do
      cons m x
      runList xs m

toList :: List a -> IO [a]
toList l = do
  r' <- readIORef (root l)
  r  <- readIORef (next r')
  s  <- readIORef (size l)
  runList r [] s
  where
    runList :: Node a -> [a] -> Int  -> IO [a]
    runList _ m 0 = return m
    runList (Node _ (Just a) u) m k = do
      n' <- readIORef u
      seq m $ runList n' (a:m) (k - 1)
    runList _ _ _ = return []

instance Show a => Show (List a) where
  show = show . unsafePerformIO . toList

main :: IO()
main = do
  l <- newList
  cons l (1 :: Int)
  cons l 2
  cons l 3
  snoc l 0
  print l
```

## ST单子

`ST`单子的功能弱于`IO`主要用于书写变量
```haskell
newtype ST s a = ST (STRep s a)
type STRep s a = State# s -> (# State# s, a #)
```
对应的操作有
```haskell
newSTRef :: a -> ST s (STRef s a)
readSTRef :: s a -> ST s a
writeSTRef :: STRef s a -> a -> ST s ()
modifySTRef :: STRef s a -> (a -> a) -> ST s ()
modifySTRef' :: STRef s a -> (a -> a) -> ST s ()
```
最后还有运行函数
```haskell
-- Control.Monad.ST
runST :: (forall s. ST s a) -> a
```
`ST`单子又称线程单子，因为`runST`是相互独立的，因此也不需要专门的原子操作。这里这个`forall`产生了二阶类型，为的就是防止`s`类型逃逸，否则就不能在随意的`s`间切换了。
因此直接在`REPL`里运行`runST (newSTRef (15 :: Int))`是不可行的

### ST单子形式下的最小子串和
```haskell
minSubArray :: (Ord a, Num a) => [a] -> Int -> a
minSubArray xs m = runST $ do
    let initSum = sum initXs
    minSum <- newSTRef initSum
    tempSum <- newSTRef initSum
    forM_ xs' $ \(x', x) -> do
        min <- readSTRef minSum
        temp <- readSTRef tempSum
        let temp' = temp + x' - x
        writeSTRef tempSum temp'
        when (temp' < min) $ writeSTRef minSum temp'
        
    readSTRef minSum
    where
        (initXs, shifted) = splitAt m xs
        xs' = zip shifted xs
```


