# 单子变换

## Kleisli
单子自身也可以构成一个范畴，叫做`Kleisli`范畴。这个范畴的特点是：
- return 是单位态射，对应`Hask`范畴的`id`
- `>=>` 是组合运算，对应`Hask`范畴的`.`

其中`>=>`的定义是
```haskell 
(>=>) :: Monad m => (a -> m b) -> (b -> m c) -> (a -> m c)
f >=> b = \x -> f x >>= g

(<=<) :: Monad m => (b -> m c) -> (a -> m b) -> (a -> m c)
f <=< g = flip (>=>)
```

## ReaderT

`Reader`单子的作用是注入全局参数，现在我们想用它包装其他单子，使得合成的新单子也拥有全局参数的功能，`ReaderT`就是为此而生

```haskell
newtype ReaderT r m a = ReaderT {runReaderT :: r -> m a}
```

可以看到`Reader`在`Hask`范畴上而`ReaderT`在`Kleisli`范畴上。

相关实例（注意内外层函数区别）：
```haskell
instance (Functor m) => Functor (ReaderT r m) where
    fmap f m = ReaderT $ \r -> fmap f (runReaderT m r)

instance (Applicative m) => Applicative (ReaderT r m) where
    -- pure :: a -> ReaderT r m a
    pure r = ReaderT $ \ _ -> pure r

    -- (<*>) :: ReaderT r m (a -> b) -> ReaderT r m a -> ReaderT r m b
    f <*> v = ReaderT $ \r -> runReaderT f r <*> runReaderT v r

instance (Monad m) => Monad (ReaderT r m) where
    -- return :: a -> ReaderT r m a
    return = pure
    -- (>>=) :: ReaderT r m a -> (a -> ReaderT r m b) -> ReaderT r m b
    m >>= k = ReaderT $\ r -> do
        a <- runReaderT m r
        runReaderT (k a) r

```

### 示例

```haskell
printEnv :: ReaderT String IO ()
printEnv = do
    ReaderT $ \env -> putStrLn ("Here' s " ++ env)

main :: IO ()
main = runReaderT printEnv "env"
```

观察`printEnv`，就是把读入的参数直接`print`出去，为了节省这种情况下的手动打包，定义函数：
```haskell
liftReaderT :: m a -> ReaderT r m a
liftReaderT m = ReaderT (const m)
```
以及类似`Reader`的辅助函数：
```haskell
ask :: Monad m => ReaderT r m r
ask = ReaderT return

local :: (r -> r)
    -> (ReaderT r m a)
    -> ReaderT r m a
local f m = ReaderT $ \r -> runReaderT m (f r)
```

新的使用示例：

```haskell
printEnv :: ReaderT String IO ()
printEnv = do
    env <- ask
    liftReaderT $ putStrLn ("Here is " ++ env)
    local (const "local env") $ do
        env' <- ask
        liftReaderT $ putStrLn ("Here is " ++ env')
```
## 统一升格接口
为了把以上提到的升格操作统一起来，定义了`MonadTrans`类：
```haskell
class MonadTrans t where
    -- | Lift a computation from the argument monad to the constructed monad.
    lift :: (Monad m) => m a -> t m a
```

这样就可以用统一的`lift`完成底层单子到外层单子的变换


## Indentity / IdentityT

Identity 是一个单子，相关定义为：
```haskell
-- | @since 4.8.0.0
instance Functor Identity where
    fmap     = coerce

-- | @since 4.8.0.0
instance Applicative Identity where
    pure     = Identity
    (<*>)    = coerce
    liftA2   = coerce

-- | @since 4.8.0.0
instance Monad Identity where
    m >>= k  = k (runIdentity m)

-- | @since 4.8.0.0
instance MonadFix Identity where
    mfix f   = Identity (fix (runIdentity . f))
```

这样我们可以发现`ReaderT r Identity` 和 `Reader r`应当是等价的，事实上很多应用单子变换的场合都是用这样的方式来定义普通的单子的。

同时，还有`IdentityT`这个单子变换，主要应用在单子变换的类型约束中，范围并不广，相关定义：
```haskell
newtype IdentityT f a = IdentityT { runIdentityT :: f a }

instance (Functor m) => Functor (IdentityT m) where
    fmap f = mapIdentityT (fmap f)
    {-# INLINE fmap #-}


instance (Applicative m) => Applicative (IdentityT m) where
    pure x = IdentityT (pure x)
    {-# INLINE pure #-}
    (<*>) = lift2IdentityT (<*>)
    {-# INLINE (<*>) #-}
    (*>) = lift2IdentityT (*>)
    {-# INLINE (*>) #-}
    (<*) = lift2IdentityT (<*)
    {-# INLINE (<*) #-}

instance (Monad m) => Monad (IdentityT m) where
#if !(MIN_VERSION_base(4,8,0))
    return = IdentityT . return
    {-# INLINE return #-}
#endif
    m >>= k = IdentityT $ runIdentityT . k =<< runIdentityT m
    {-# INLINE (>>=) #-}
    fail msg = IdentityT $ fail msg
    {-# INLINE fail #-}

-- | Lift a unary operation to the new monad.
mapIdentityT :: (m a -> n b) -> IdentityT m a -> IdentityT n b
mapIdentityT f = IdentityT . f . runIdentityT
{-# INLINE mapIdentityT #-}
```

## StateT
`StateT` 是 `State` 对应的单子变换，解决的是给其他单子添加状态的问题。

```haskell
newtype StateT s m a = StateT { runStateT :: s -> m (a,s) }

-- | Evaluate a state computation with the given initial state
-- and return the final value, discarding the final state.
--
-- * @'evalStateT' m s = 'liftM' 'fst' ('runStateT' m s)@
evalStateT :: (Monad m) => StateT s m a -> s -> m a
evalStateT m s = do
    ~(a, _) <- runStateT m s
    return a
{-# INLINE evalStateT #-}

-- | Evaluate a state computation with the given initial state
-- and return the final state, discarding the final value.
--
-- * @'execStateT' m s = 'liftM' 'snd' ('runStateT' m s)@
execStateT :: (Monad m) => StateT s m a -> s -> m s
execStateT m s = do
    ~(_, s') <- runStateT m s
    return s'
{-# INLINE execStateT #-}

-- | Map both the return value and final state of a computation using
-- the given function.
--
-- * @'runStateT' ('mapStateT' f m) = f . 'runStateT' m@
mapStateT :: (m (a, s) -> n (b, s)) -> StateT s m a -> StateT s n b
mapStateT f m = StateT $ f . runStateT m
{-# INLINE mapStateT #-}

-- | @'withStateT' f m@ executes action @m@ on a state modified by
-- applying @f@.
--
-- * @'withStateT' f m = 'modify' f >> m@
withStateT :: (s -> s) -> StateT s m a -> StateT s m a
withStateT f m = StateT $ runStateT m . f
{-# INLINE withStateT #-}

instance (Functor m) => Functor (StateT s m) where
    fmap f m = StateT $ \ s ->
        fmap (\ ~(a, s') -> (f a, s')) $ runStateT m s
    {-# INLINE fmap #-}

instance (Functor m, Monad m) => Applicative (StateT s m) where
    pure a = StateT $ \ s -> return (a, s)
    {-# INLINE pure #-}
    StateT mf <*> StateT mx = StateT $ \ s -> do
        ~(f, s') <- mf s
        ~(x, s'') <- mx s'
        return (f x, s'')
    {-# INLINE (<*>) #-}
    m *> k = m >>= \_ -> k
    {-# INLINE (*>) #-}

instance (Functor m, MonadPlus m) => Alternative (StateT s m) where
    empty = StateT $ \ _ -> mzero
    {-# INLINE empty #-}
    StateT m <|> StateT n = StateT $ \ s -> m s `mplus` n s
    {-# INLINE (<|>) #-}

instance (Monad m) => Monad (StateT s m) where
#if !(MIN_VERSION_base(4,8,0))
    return a = StateT $ \ s -> return (a, s)
    {-# INLINE return #-}
#endif
    m >>= k  = StateT $ \ s -> do
        ~(a, s') <- runStateT m s
        runStateT (k a) s'
    {-# INLINE (>>=) #-}
    fail str = StateT $ \ _ -> fail str
    {-# INLINE fail #-}
```

相对应的，也有升格底层操作的函数：
```haskell
instance MonadTrans (StateT s) where
    lift m = StateT $ \ s -> do
        a <- m
        return (a, s)
    {-# INLINE lift #-}
```

为了方便使用，`Control.Monad.Trans.State.Strict`同时定义了一系列的辅助函数：
```haskell
-- | Construct a state monad computation from a function.
-- (The inverse of 'runState'.)
state :: (Monad m)
      => (s -> (a, s))  -- ^pure state transformer
      -> StateT s m a   -- ^equivalent state-passing computation
state f = StateT (return . f)
{-# INLINE state #-}


-- | Fetch the current value of the state within the monad.
get :: (Monad m) => StateT s m s
get = state $ \ s -> (s, s)
{-# INLINE get #-}

-- | @'put' s@ sets the state within the monad to @s@.
put :: (Monad m) => s -> StateT s m ()
put s = state $ \ _ -> ((), s)
{-# INLINE put #-}

-- | @'modify' f@ is an action that updates the state to the result of
-- applying @f@ to the current state.
--
-- * @'modify' f = 'get' >>= ('put' . f)@
modify :: (Monad m) => (s -> s) -> StateT s m ()
modify f = state $ \ s -> ((), f s)
{-# INLINE modify #-}

-- | A variant of 'modify' in which the computation is strict in the
-- new state.
--
-- * @'modify'' f = 'get' >>= (('$!') 'put' . f)@
modify' :: (Monad m) => (s -> s) -> StateT s m ()
modify' f = do
    s <- get
    put $! f s
{-# INLINE modify' #-}

-- | Get a specific component of the state, using a projection function
-- supplied.
--
-- * @'gets' f = 'liftM' f 'get'@
gets :: (Monad m) => (s -> a) -> StateT s m a
gets f = state $ \ s -> (f s, s)
{-# INLINE gets #-}

-- | Evaluate a state computation with the given initial state
-- and return the final value, discarding the final state.
--
-- * @'evalStateT' m s = 'liftM' 'fst' ('runStateT' m s)@
evalStateT :: (Monad m) => StateT s m a -> s -> m a
evalStateT m s = do
    (a, _) <- runStateT m s
    return a
{-# INLINE evalStateT #-}

-- | Evaluate a state computation with the given initial state
-- and return the final state, discarding the final value.
--
-- * @'execStateT' m s = 'liftM' 'snd' ('runStateT' m s)@
execStateT :: (Monad m) => StateT s m a -> s -> m s
execStateT m s = do
    (_, s') <- runStateT m s
    return s'
{-# INLINE execStateT #-}

-- | Map both the return value and final state of a computation using
-- the given function.
--
-- * @'runStateT' ('mapStateT' f m) = f . 'runStateT' m@
mapStateT :: (m (a, s) -> n (b, s)) -> StateT s m a -> StateT s n b
mapStateT f m = StateT $ f . runStateT m
{-# INLINE mapStateT #-}

-- | @'withStateT' f m@ executes action @m@ on a state modified by
-- applying @f@.
--
-- * @'withStateT' f m = 'modify' f >> m@
withStateT :: (s -> s) -> StateT s m a -> StateT s m a
withStateT f m = StateT $ runStateT m . f
{-# INLINE withStateT #-}
```

### 例子

这个程序数五行输入中共有多少个字符：
```haskell
module TestStateT where
import Control.Monad.Trans.State.Strict
import Control.Monad.Trans.Class

process :: Int -> StateT Int IO ()
process 0 = do
  result <- get
  lift $ print result

process t = do
  a <- lift getLine
  modify $ \x -> x + (length a)
  process (t - 1)

main :: IO ()
main = evalStateT (process 5) 0
```


这是一个使用`MonadRandom`写的`1A2B`猜测程序：
```haskell
module Main where
import Control.Monad.Trans.Class
import Control.Monad.Random
import Data.List
import System.Random

type Permutation = [Int]

readInt :: IO Int
readInt = fmap read $ getLine

perms :: Int -> [Permutation]
perms n = do go n [[]]
  where
    ns = [0..9]
    go 0 ps = ps
    go n ps = go (n - 1) [x : p | x <- ns, p <- ps , notElem x p]

guess :: (RandomGen g) => [Permutation] -> RandT g IO [Permutation]
guess xs
  | length xs <= 1 = return xs
  | otherwise = do
      g <- uniform xs
      (a, b) <- lift $ do
        putStrLn . concat $ map show g
        putStrLn "A?"
        a <- readInt
        putStrLn "B?"
        b <- readInt
        return (a, b)
      let allMark = mark xs g
      let allZipped = filter (\z -> snd z == (a, b)) $ zip xs allMark
      guess $ map fst allZipped

mark :: [Permutation] -> Permutation -> [(Int, Int)]
mark [] _ = []
mark (x : xs) p = (markA x p, markB x p - markA x p) : mark xs p
  where
    markA x p = sum $ zipWith (\x y -> if x == y then 1 else 0) x p
    markB x p = sum $ map (\x -> if x `elem` p then 1 else 0) x

main :: IO ()
main = do
  putStrLn "how many numbers in a permutation(1~9)"
  n <- readInt
  if (n < 0 || n > 9)
  then putStrLn "Well ..."
  else do
    g <- getStdGen
    evalRandT (guess (perms n) >>= lift . print . concat) g
```

## 升格辅助语法

多次使用单子变换的手段可以获得叠加了多层信息的单子栈，但是获取内层函数却需要连续的`lift`。为此，很多库添加了类似于以下形式的声明：
```haskell
instance (MonadRandom m) => MonadRandom (StateT s m) where
    ...

instance (MonadRandom m) => MonadRandom (ReaderT s m) where
    ...
```
即在外层提供了内层函数`lift`后的结果。然后由于单子性质可以传导，只要某一层包含了一个`MonadRandom`，那么外层也会成为`MonadRandom`的实例，那么即使在形同`StateT [Perm] (RandT g IO) ()`样子的单子栈的最外层，也可以直接使用`uniform`函数而不需要显式的写出`lift`

另一种解决方法的代表是`MonadIO`, 在`transformer`中有这样的定义：
```haskell
class Monad m => MonadIO m where
    liftIO :: IO a -> m a

instance MonadIO IO where
    liftIO = id

instance (MonadIO m) => MonadIO (StateT s m) where
    liftIO = lift . liftIO

instance (MonadIO m) => MonadIO (ReaderT r m) where
    liftIO = lift. liftIO

...
```
这样只要单子栈是使用定义过`MonadIO`类型的单子变换搭建起来的，`liftIO`函数就可以把底层的`IO`最外面来。


### 依赖类型约束的单子变换

还有一些定义稍有不同的单子变换：
```haskell
-- Control.Monad.Reader.Class

class Monad m => MonadReader r m | m -> r where
    ask :: m r
    local :: (r -> r) -> m a -> m a
    reader :: (r -> a) -> m a

class Monad m => MonadState s m | m -> s where
    get :: m s
    put :: s -> m ()
    state :: (s -> (a, s)) -> m a
```
这里使用了依赖类型的写法，即`s`的类型由`m`唯一决定，比如说：
```haskell
instance MonadReader r ((->) r) where
    ask = id
    local f m = m . f
    reader = id
```
这就指定了`Reader`的`ReaderMonad`的`r`的一致性，这样使得类型在不同层单子栈中传递成为可能，更具体的：
```haskell
instance MonadReader r m => MonadReader r (StateT s m) where
    ask = lift ask
    local = mapStateT . local
    reader = lift . reader

mapStateT :: (m (a, s) -> n (b, s)) -> StateT s m a -> StateT s n b
mapStateT f m = StateT $ f . runStateT m
```
这样对于`StateT s (ReaderT r IO) a`这样的单子栈, 编译器就能知道`r`应该是全局中`Reader`的类型。

### 类型家族约束的单子变换

